plugins {
    id 'idea'
    id 'signing'
    id 'groovy'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

task groovydocJar(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    archiveClassifier = 'javadoc'
}

task exportJavadoc(type: Copy, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    into 'javadoc'
}

artifacts {
    archives jar
    archives sourceJar
    archives groovydocJar
}

//********* artifact signing *********
if (gradle.startParameter.taskNames.contains("uploadArchives") || gradle.startParameter.taskNames.contains("publishPlugins")) {
    signing {
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

group = 'com.github.rholder.gradle'

publishing {
    publications {
        release(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}
